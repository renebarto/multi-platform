project(unit-test-c++ CXX)

include(setup_target_properties_library)
include(show_target_properties)
include(display_list)

message("\nunit-test-c++\n")

set(PACKAGE_NAME ${PROJECT_NAME})
set(TARGET_NAME ${PROJECT_NAME})
set(PACKAGE_DESCRIPTION "Unit test framework")
set(PACKAGE_VERSION_MAJOR 1)
set(PACKAGE_VERSION_MINOR 0)
set(PACKAGE_VERSION_MICRO 0)
set(PACKAGE_VERSION ${PACKAGE_VERSION_MAJOR}.${PACKAGE_VERSION_MINOR}.${PACKAGE_VERSION_MICRO})

set(COMPONENTS_UNITTESTCPP_DEFINITIONS
    ${COMPILER_DEFINITIONS}
    UNIT_TEST_CPP_DYNAMIC)

set(COMPONENTS_UNITTESTCPP_OPTIONS
    ${COMPILER_OPTIONS_CXX}
    )

set(COMPONENTS_UNITTESTCPP_INCLUDE_DIRS
    ${PROJECT_SOURCE_DIR}/include
    ${PROJECT_SOURCE_DIR}/export
    )

set(COMPONENTS_UNITTESTCPP_LINK_OPTIONS
    ${LINK_RPATH})
list_to_string(COMPONENTS_UNITTESTCPP_LINK_OPTIONS COMPONENTS_UNITTESTCPP_LINK_OPTIONS_STRING)

set(COMPONENTS_UNITTESTCPP_DEPENDENCIES
    osal
    )

set(COMPONENTS_UNITTESTCPP_LIBS
    ${CMAKE_THREAD_LIBS_INIT}
    ${COMPONENTS_UNITTESTCPP_DEPENDENCIES})

file(GLOB_RECURSE COMPONENTS_UNITTESTCPP_SOURCES src/*.cpp)
file(GLOB_RECURSE COMPONENTS_UNITTESTCPP_INCLUDES_EXPORT export/*.h)
file(GLOB_RECURSE COMPONENTS_UNITTESTCPP_INCLUDES include/*.h)

set(COMPONENTS_UNITTESTCPP_INCLUDES
    ${COMPONENTS_UNITTESTCPP_INCLUDES_EXPORT}
    ${COMPONENTS_UNITTESTCPP_INCLUDES})

display_list("Defines                     : " ${COMPONENTS_UNITTESTCPP_DEFINITIONS} )
display_list("Compiler options            : " ${COMPONENTS_UNITTESTCPP_OPTIONS} )
display_list("Source files                : " ${COMPONENTS_UNITTESTCPP_SOURCES} )
display_list("Include files               : " ${COMPONENTS_UNITTESTCPP_INCLUDES} )
display_list("Include dirs                : " ${COMPONENTS_UNITTESTCPP_INCLUDE_DIRS} )
display_list("Link libs                   : " ${COMPONENTS_UNITTESTCPP_LIBS} )
display_list("Linker options              : " ${COMPONENTS_UNITTESTCPP_LINK_OPTIONS} )
display_list("Dependencies                : " ${COMPONENTS_UNITTESTCPP_DEPENDENCIES} )

# add the library
add_library(${PROJECT_NAME} SHARED ${COMPONENTS_UNITTESTCPP_SOURCES} ${COMPONENTS_UNITTESTCPP_INCLUDES})
target_link_libraries(${PROJECT_NAME} ${COMPONENTS_UNITTESTCPP_LIBS})
target_include_directories(${PROJECT_NAME} PUBLIC ${COMPONENTS_UNITTESTCPP_INCLUDE_DIRS})
target_compile_definitions(${PROJECT_NAME} PRIVATE UNIT_TEST_CPP_BUILD)
target_compile_definitions(${PROJECT_NAME} PUBLIC ${COMPONENTS_UNITTESTCPP_DEFINITIONS})
target_compile_options(${PROJECT_NAME} PRIVATE ${COMPONENTS_UNITTESTCPP_OPTIONS})
set_target_properties(${PROJECT_NAME} PROPERTIES LINK_FLAGS "${COMPONENTS_UNITTESTCPP_LINK_OPTIONS_STRING}")
set_target_properties(${PROJECT_NAME} PROPERTIES VERSION ${PACKAGE_VERSION_MAJOR}.${PACKAGE_VERSION_MINOR}.${PACKAGE_VERSION_MICRO})
set_target_properties(${PROJECT_NAME} PROPERTIES SOVERSION ${PACKAGE_VERSION_MAJOR})
set_target_properties(${PROJECT_NAME} PROPERTIES OUTPUT_NAME unit-test-c++)
setup_target_properties_library(${PROJECT_NAME})

show_target_properties(${PROJECT_NAME})

configure_file(
    ${CMAKE_CURRENT_SOURCE_DIR}/unit-test-c++.pc.in
    ${CMAKE_CURRENT_SOURCE_DIR}/unit-test-c++.pc
    @ONLY)

# uninstall target
configure_file(
    ${CMAKE_CURRENT_SOURCE_DIR}/uninstall.cmake.in
    ${CMAKE_CURRENT_BINARY_DIR}/uninstall.cmake
    IMMEDIATE @ONLY)

add_custom_target(uninstall_${PROJECT_NAME}
    COMMAND ${CMAKE_COMMAND} -DCOMPONENT=${PROJECT_NAME} -P ${CMAKE_CURRENT_BINARY_DIR}/uninstall.cmake
    DEPENDS ${PROJECT_NAME}
    COMMENT "Uninstalling ${PROJECT_NAME}")

add_custom_target(install_${PROJECT_NAME}
    COMMAND ${CMAKE_COMMAND} -DCOMPONENT=${PROJECT_NAME} -P ${CMAKE_BINARY_DIR}/cmake_install.cmake
    DEPENDS ${PROJECT_NAME}
    COMMENT "Installing ${PROJECT_NAME}")

add_dependencies(install-components install_${PROJECT_NAME})

add_dependencies(uninstall-components uninstall_${PROJECT_NAME})

install(TARGETS ${PROJECT_NAME}
    ARCHIVE DESTINATION ${TOOLS_LIB_DIR}
    LIBRARY DESTINATION ${TOOLS_LIB_DIR}
    COMPONENT ${PROJECT_NAME})

install(FILES ${CMAKE_CURRENT_SOURCE_DIR}/unit-test-c++.pc
    DESTINATION ${TOOLS_PKG_CONFIG_DIR}
    COMPONENT ${PROJECT_NAME})
